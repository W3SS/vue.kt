buildscript {
  ext.kotlin_version = '1.2.21'
  ext.kotlinx_html_version = '0.6.8'
  ext.dokka_version = '0.9.16-eap-2' // not compatible with Gradle 4.5 yet

  repositories {
    mavenCentral()
    maven {
      // dokka
      url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
  }
}

plugins {
  id 'com.jfrog.bintray' version '1.8.0'
}

group 'de.frozenice'
version '0.0.1'

apply plugin: 'kotlin2js'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
}

compileKotlin2Js {
  kotlinOptions.metaInfo = true
  kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
  kotlinOptions.sourceMap = true
  kotlinOptions.moduleKind = 'commonjs'
  kotlinOptions.main = 'call'
}

compileTestKotlin2Js {
  kotlinOptions.metaInfo = true
  kotlinOptions.outputFile = "$project.buildDir.path/js-tests/${project.name}-tests.js"
  kotlinOptions.sourceMap = true
  kotlinOptions.moduleKind = 'commonjs'
  kotlinOptions.main = 'call'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
  classifier = 'javadoc'
  from dokka.outputDirectory
}

artifacts {
  archives sourcesJar, javadocJar
}

dokka {
  kotlinTasks {
    compileKotlin2Js
  }
  includeNonPublic = false
  reportUndocumented = false

  outputFormat = 'javadoc'

  jdkVersion = 8 // seems to do nothing
  impliedPlatforms = ['JS'] // seems to do nothing
  noStdlibLink = false // seems to do nothing
  linkMapping { // seems to do nothing
    dir = "src/main/kotlin"
    url = "https://github.com/frozenice/vue.kt/blob/master/src/main/kotlin"
    suffix = "#L"
  }
}

def pomConfig = {
  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
    }
  }
  developers {
    developer {
      id 'frozenice'
      name 'David Kirstein'
      email 'frozenice@frozenice.de'
    }
  }

  scm {
    url 'https://github.com/frozenice/vue.kt.git'
  }
}

publishing {
  publications {
    JsLibrary(MavenPublication) {
      groupId project.group
      artifactId project.name
      version project.version

      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Vue.js via Kotlin')
        root.appendNode('name', project.name)
        root.appendNode('url', 'https://github.com/frozenice/vue.kt')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  // stored in ~/.gradle/gradle.properties
  user = getProperty('bintray.user')
  key = getProperty('bintray.apikey')

  publications = ['JsLibrary']
  publish = true

  pkg {
    repo = 'kotlin'
    name = project.name
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/frozenice/vue.kt.git'

    publicDownloadNumbers = true

    version {
      name = project.version

      gpg {
        sign = true // use bintray's keys
      }
    }
  }
}
